Class {
	#name : #StepOverException,
	#superclass : #DebuggerTests,
	#category : #DebuggerTests
}

{ #category : #pages }
StepOverException >> step1 [
	self step2.
]

{ #category : #pages }
StepOverException >> step2 [
	[ self step3 ] on: Notification do: [ ^2 ].
]

{ #category : #pages }
StepOverException >> step3 [
	self step4.
	^4
]

{ #category : #pages }
StepOverException >> step4 [
	self notify: 'hey'
]

{ #category : #tests }
StepOverException >> stepA1 [
	self stepA2.
	^ 42.
]

{ #category : #tests }
StepOverException >> stepA2 [
	^ True
]

{ #category : #tests }
StepOverException >> stepB1 [
	self stepB2.
	^42.
]

{ #category : #tests }
StepOverException >> stepB2 [
	self stepB3
]

{ #category : #tests }
StepOverException >> stepB3 [
	^ 42
]

{ #category : #tests }
StepOverException >> testErrorSignalledDuringStepOverShouldBeCaught [
	"An Error signaled while being steppedOver should not be unhandled"
	self settingUpSessionAndProcessAndContextForBlock: [ Error signal: 'hey'. ].
	self shouldnt: [[ session interruptedProcess isTerminated ] whileFalse: [ session stepOver. ]] raise: Error.
]

{ #category : #tests }
StepOverException >> testStepOver [
	"Stepping over a message node brings the execution to the next node in the same method."
	| node |
	self settingUpSessionAndProcessAndContextForBlock: [ self stepA1 ].
	session stepInto.
	session stepInto.
	"Reached stepA1"
	"Checking that the execution is at the 'self stepA2' node of the stepA1 method"
	self assert: session interruptedContext method equals: self class >>#stepA1.
	node := self class >>#stepA1 sourceNodeForPC: session interruptedContext pc.
	self assert: node receiver isSelf.
	self assert: node selector equals: #stepA2.
	session stepOver.
	"Checking if the stepOver stepped over the call to stepA2 and brought the execution to the return node of stepA1"
	self assert: session interruptedContext method equals: self class >>#stepA1.
	self assert: (StepOverException>>#stepA1 sourceNodeForPC: session interruptedContext pc) isReturn.
]

{ #category : #tests }
StepOverException >> testStepOverDoesNotUnderstand [
	"Stepping over a message not understood should not raise an unhandled exception"
	self settingUpSessionAndProcessAndContextForBlock: [ self messageNotUnderstood].
	self shouldnt: [[ session interruptedProcess isTerminated ] whileFalse: [ session stepOver. ]] raise: Exception.
]

{ #category : #tests }
StepOverException >> testStepOverHalt [
	"Stepping over a self halt should not raise an unhandled exception"
	self settingUpSessionAndProcessAndContextForBlock: [ self halt. ].
	self shouldnt: [[ session interruptedProcess isTerminated ] whileFalse: [ session stepOver. ]] raise: Exception.
]

{ #category : #tests }
StepOverException >> testStepOverLastNodeOfContext [
	"Stepping over the last node of a method brings the execution to the method node of that method."
	| node |
	self settingUpSessionAndProcessAndContextForBlock: [ self stepB1 ].
	session stepInto.
	session stepInto.
	"Reached stepB1"
	session stepInto.
	"Reached stepB2"
	"Checking that the execution is at the 'self stepB3' node of the stepB2 method"
	self assert: session interruptedContext method equals: self class >>#stepB2.
	node := self class >>#stepB2 sourceNodeForPC: session interruptedContext pc.
	self assert: node receiver isSelf.
	self assert: node selector equals: #stepB3.
	session stepOver.
	"Checking that after the stepOver, the execution is at the method node of the stepB2 method"
	self assert: session interruptedContext method equals: self class >>#stepB2.
	node := self class >>#stepB2 sourceNodeForPC: session interruptedContext pc.
	self assert: node isMethod.
]

{ #category : #tests }
StepOverException >> testStepOverNonErrorExceptionSignalWithHandlerDeeperInTheContextStack [
	"Context stack (from top to bottom) of the execution:
	self step4 | signal a Notification exception
	self step3 | just calls step 4. Point at which this test performs the stepOver
	self step2 | has a handler for Notification exceptions
	self step1 | just calls step2
	
	When doing a stepOver at step3, the session jumps to step1.
	The Notification exception was used because it is an exception that is not a subclass of Error, and iis therefore not caught by the handler context stepOver inserts between step2 and 3 (which handles Error)
	"
	self settingUpSessionAndProcessAndContextForBlock: [ self step1 ].
	session stepInto.
	session stepInto.
	session stepInto.
	session stepInto.
	session stepInto.
	session stepInto.
	session stepInto.
	session stepInto.
	"Reached the beginning of the step3 method call. Now doing a stepOver"
	session stepOver.
	"The DebugSession jumped to the step1 method call"
	self assert: session interruptedContext method equals: self class >>#step1
]
